import java.util.regex.Matcher

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE")
    }
}

plugins {
    id("org.hidetake.ssh").version("2.9.0")
    id("net.researchgate.release").version("2.8.1")
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

repositories {
    mavenCentral()
}

release {
	git.requireBranch = null
    versionPatterns = [
        (/\d+\.\d+\.\d+-SNAPSHOT/): { Matcher m, Project p ->
            def parts = m.group(0).split(/\./)
            def major = parts[0].toInteger()
            def minor = parts[1].toInteger()
            def patch = parts[2].split("-")[0].toInteger() + 1
            return "${major}.${minor}.${patch}-SNAPSHOT"
        }
    ]
}

bootJar {
    baseName = 'gs-spring-boot'
    version =  '0.1.0'
}


task upVersion(dependsOn: ['release']) {
    doLast {
        println "Vers√£o atual: ${project.version}"
        def propsFile = file('src/main/resources/version.properties')
        propsFile.text = "version=${project.version}"
    }
}

task createWar(type : War){
    destinationDir = file("$buildDir")
    baseName = buildBaseName
    version = buildVersion
    caseSensitive = true
    from "src"
}

ssh.settings {
	knownHosts = allowAnyHosts
}

remotes {
  ContaigeClaud {
    host = 'ag-br1-17.conteige.cloud' 
    port = 28626
    user = 'application'
    identity = file("${System.properties['user.home']}/.ssh/guimas_rsa_id") 
  }
}  

task deploy {
  doLast {
    ssh.run {
      session(remotes.ContaigeClaud) {
        put from: "$buildDir/${buildBaseName}-${buildVersion}.war", into: 'appserver/webapps/ROOT.war'
      }
    }
  }
}

task createWarAndDeploy(dependsOn: ['createWar', 'deploy']) {
    // empty task body, dependsOn already handles the dependencies
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
   //compile("org.flywaydb:flyway-core:5.0.7")
    implementation("org.postgresql:postgresql")
    implementation("com.mashape.unirest:unirest-java:1.4.9")
	implementation("com.google.api-client:google-api-client:1.32.1")
	
    
    testImplementation("junit:junit")
}